<!DOCTYPE html>
<html>
<head>
    <title>Mig 21 The JukeBox</title>
    <style>
        .container {
            display: flex;
            justify-content: space-around;
            align-items: center;
            margin-bottom: 20px;
        }

        .code-box {
            width: 100%;
            height: 200px;
            margin-bottom: 20px;
            padding: 10px;
            border: 1px solid #ddd;
            background-color: #f4f4f4;
            overflow-y: scroll;
            white-space: pre-wrap;
        }
    </style>
</head>
<body>
    <div class="container">
        <img src="Mig21TheJukeBox.png" alt="Screenshot" width="400">
        <video width="400" controls>
            <source src="Final1214Mig21Juke.mp4" type="video/mp4">
        </video>
    </div>

    <textarea class="code-box" readonly>
        #define POT_PIN A0
        #define BUTTON_PIN1 12
        #define BUTTON_PIN2 27
        #define BUTTON_PIN3 33
        
        void setup() {
          pinMode(POT_PIN, INPUT);
          pinMode(BUTTON_PIN1, INPUT);
          pinMode(BUTTON_PIN2, INPUT);
          pinMode(BUTTON_PIN3, INPUT);
        
          Serial.begin(9600);
        }
        
        void loop() {
          int valuePot = analogRead(POT_PIN);
          int valueButton1 = digitalRead(BUTTON_PIN1);
          int valueButton2 = digitalRead(BUTTON_PIN2);
          int valueButton3 = digitalRead(BUTTON_PIN3);
        
          Serial.print(valuePot);
          Serial.print(',');
          Serial.print(valueButton1);
          Serial.print(',');
          Serial.print(valueButton2);
          Serial.print(',');
          Serial.println(valueButton3);
          Serial.println();
        
          delay(150);
        }
    </textarea>

    <textarea class="code-box" readonly>
        import processing.serial.*;
        import processing.sound.*;
        
        PImage planeImage, missileImage;
        float planeX;
        Serial myPort;
        
        SoundFile gunSound, missileSound, newMissileHitSound, specialAttackSound, newGunHitSound;
        
        ArrayList<PVector> bullets;
        ArrayList<PVector> missiles;
        boolean specialAttack = false;
        
        PVector enemy; // Red Ball Location
        float enemySpeedX, enemySpeedY; // Red Velocity
        boolean enemyHitBySpecialAttack = false;
        
        boolean[] currentButtonState = {false, false, false};
        boolean[] lastButtonState = {false, false, false};
        
        void setup() {
            size(800, 600);
            initSerial();
            planeImage = loadImage("mig21.png");
            if (planeImage != null) {
                planeImage.resize(800 / 10, 0);
            }
            planeX = width / 2 - (planeImage != null ? planeImage.width / 2 : 0);
        
            missileImage = loadImage("missile.png");
            missileImage.resize(missileImage.width * 3 / 4, 0);
        
            bullets = new ArrayList<PVector>();
            missiles = new ArrayList<PVector>();
        
            gunSound = new SoundFile(this, "gunfire.wav");
            missileSound = new SoundFile(this, "missile.wav");
            newMissileHitSound = new SoundFile(this, "Dramatic.mp3");
            specialAttackSound = new SoundFile(this, "ult.wav");
            newGunHitSound = new SoundFile(this, "Cooking.wav");
        
            enemy = new PVector(random(width), random(height));
            enemySpeedX = random(2, 4); // X Axis Velocity
            enemySpeedY = random(2, 4); // Y Axis Velocity
        
            frameRate(60);
        }
        
        void draw() {
            background(255);
        
            if (specialAttack) {
                background(0);
                specialAttack = false;
                enemyHitBySpecialAttack = false;
                gunSound.stop();
                newMissileHitSound.stop();
                newGunHitSound.stop();
            }
        
            planeX = constrain(planeX, 0, width - planeImage.width);
            image(planeImage, planeX, height - planeImage.height);
        
            updateBullets();
            updateMissiles();
            updateEnemy();
        
            for (int i = 0; i < 3; i++) {
                if (currentButtonState[i] && !lastButtonState[i]) {
                    triggerButtonAction(i);
                }
                lastButtonState[i] = currentButtonState[i];
            }
        }
        
        void initSerial() {
            if (myPort != null) {
                myPort.stop();
            }
            myPort = new Serial(this, "/dev/cu.SLAB_USBtoUART", 9600);
            myPort.bufferUntil('\n');
        }
        
        void updateBullets() {
            fill(0, 0, 255);
            noStroke();
            for (int i = bullets.size() - 1; i >= 0; i--) {
                PVector bullet = bullets.get(i);
                bullet.y -= 5;
                if (bullet.y < 0) {
                    bullets.remove(i);
                } else {
                    ellipse(bullet.x, bullet.y, 10, 20);
                }
            }
        }
        
        void updateMissiles() {
            for (int i = missiles.size() - 1; i >= 0; i--) {
                PVector missile = missiles.get(i);
                missile.y -= 6;
                if (missile.y < 0) {
                    missiles.remove(i);
                } else {
                    image(missileImage, missile.x, missile.y, missileImage.width / 2, missileImage.height / 2);
                }
            }
        }
        
        void updateEnemy() {
            enemy.x += enemySpeedX;
            enemy.y += enemySpeedY;
        
            if (enemy.x > width || enemy.x < 0) {
                enemySpeedX *= -1;
            }
            if (enemy.y > height || enemy.y < 0) {
                enemySpeedY *= -1;
            }
        
            fill(255, 0, 0);
            ellipse(enemy.x, enemy.y, 30, 30);
        
            checkCollisionWithEnemy();
        }
        
        void checkCollisionWithEnemy() {
            for (int i = bullets.size() - 1; i >= 0; i--) {
                PVector bullet = bullets.get(i);
                if (dist(bullet.x, bullet.y, enemy.x, enemy.y) < 15) {
                    if (!enemyHitBySpecialAttack && !newGunHitSound.isPlaying()) {
                        stopAllSoundsExcept(newGunHitSound);
                        newGunHitSound.play(); // New Sound Play When Hit By Bullets
                    }
                    bullets.remove(i);
                    resetEnemy();
                    break;
                }
            }
        
            for (int i = missiles.size() - 1; i >= 0; i--) {
                PVector missile = missiles.get(i);
                float missileCenterX = missile.x + missileImage.width / 4;
                float missileCenterY = missile.y + missileImage.height / 4;
        
                if (dist(missileCenterX, missileCenterY, enemy.x, enemy.y) < 15) {
                    if (!enemyHitBySpecialAttack && !newMissileHitSound.isPlaying()) {
                        stopAllSoundsExcept(newMissileHitSound);
                        newMissileHitSound.play(); // New Sound Play When Hit By Missiles
                    }
                    missiles.remove(i);
                    resetEnemy();
                    break;
                }
            }
        }
        
        void resetEnemy() {
            enemy = new PVector(random(width), random(height));
            enemySpeedX = random(2, 4);
            enemySpeedY = random(2, 4);
        }
        
        void triggerButtonAction(int buttonIndex) {
            if (buttonIndex == 0) { // Ultimate
                specialAttack = true;
                stopAllSounds();
                specialAttackSound.play();
            } else if (buttonIndex == 1) { // Missile
                missiles.add(new PVector(planeX + planeImage.width / 2, height - planeImage.height));
                missileSound.play();
            } else if (buttonIndex == 2) { // Bullet
                bullets.add(new PVector(planeX + planeImage.width / 2, height - planeImage.height));
                gunSound.play();
            }
        }
        
        void stopAllSoundsExcept(SoundFile soundToKeep) {
            SoundFile[] sounds = {gunSound, missileSound, newMissileHitSound, specialAttackSound, newGunHitSound};
            for (SoundFile sound : sounds) {
                if (sound != soundToKeep && sound.isPlaying()) {
                    sound.stop();
                }
            }
        }
        
        void stopAllSounds() {
            SoundFile[] sounds = {gunSound, missileSound, newMissileHitSound, specialAttackSound, newGunHitSound};
            for (SoundFile sound : sounds) {
                if (sound.isPlaying()) {
                    sound.stop();
                }
            }
        }
        
        void serialEvent(Serial myPort) {
            try {
                String inData = myPort.readStringUntil('\n');
                if (inData != null) {
                    inData = trim(inData);
                    int[] inputs = int(split(inData, ','));
                    if (inputs.length >= 4) {
                        planeX = map(inputs[0], 0, 4095, 0, width - planeImage.width);
                        for (int i = 0; i < 3; i++) {
                            currentButtonState[i] = inputs[i + 1] == 1;
                        }
                    }
                }
            } catch (Exception e) {
                println("Serial Event Error: " + e.getMessage());
            }
        }
    </textarea>
</body>
</html>
